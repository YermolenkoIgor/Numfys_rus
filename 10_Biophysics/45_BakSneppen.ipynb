{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "<style>\n",
    "@import url(https://www.numfys.net/static/css/nbstyle.css);\n",
    "</style>\n",
    "<a href=\"https://www.numfys.net\"><img class=\"logo\" /></a>\n",
    "# Модель эволюции Бака-Снеппена\n",
    "\n",
    "### Examples - Biophysics\n",
    "<section class=\"post-meta\">\n",
    "By Thorvald M. Ballestad, Niels Henrik Aase, Jenny Lunde, Sondre Lundemo, and Jon Andreas Støvneng.\n",
    "</section>\n",
    "Last edited: October 6th 2020.\n",
    "\n",
    "---\n",
    "\n",
    "Этот блокнот несколько отличается от нашего обычного содержимого.\n",
    "Рассматриваемая проблема и цифры довольно просты.\n",
    "Цель блокнота - продемонстрировать язык Джулия, язык, который появился в последние несколько лет.\n",
    "Джулия в какой-то степени уже закрепилась в научном сообществе и, скорее всего, продолжит расширяться в ближайшие годы.\n",
    "Несмотря на множество чудес Python, самая большая проблема заключается в том, что он может быть медленным.\n",
    "Использование NumPy происходит чрезвычайно быстро, так как за кулисами он представляет собой высоко оптимизированный скомпилированный код и использует преимущества таких известных библиотек, как BLAS и LAPACK.\n",
    "Однако иногда использование NumPy может быть очень громоздким или невозможным, и приходится использовать циклы Python.\n",
    "В этих сценариях другие языки могут быть более подходящими.\n",
    "Многие сообщества, как в академических кругах, так и в промышленности, используют C++ из-за его скорости и мощных функций, хотя его разработка может быть как утомительной, так и просто слишком сложной.\n",
    "Кроме того, без должных знаний можно было бы писать медленный код и на C++.\n",
    "Фортран также является популярной альтернативой, особенно в научном сообществе.\n",
    "Поскольку первая версия была выпущена более 60 лет назад, это один из старых языков, все еще широко используемых в вычислительной физике.\n",
    "Он ценится за свою скорость, но может напугать и сбить с толку новых разработчиков.\n",
    "Гибридные варианты, такие как использование Фортрана только для внутреннего цикла, обертывание его в Python, являются одним из возможных методов, о котором вы можете прочитать подробнее [здесь](https://nbviewer.jupyter.org/urls/www.numfys.net/media/notebooks/fortran_to_python.ipynb) [[1]](#f2py) и [здесь](https://nbviewer.jupyter.org/urls/www.numfys.net/media/notebooks/Monte_Carlo_Ising.ipynb) [[2]](#monte_carlo).\n",
    "\n",
    "Джулия - довольно молодой язык, с огромным потенциалом.\n",
    "Она направлена на то, чтобы занять промежуточное положение между Python, с одной стороны, и Fortran и C++, с другой.\n",
    "На этом мы реализуем модель эволюции Бака-Снеппена в Джулии.\n",
    "\n",
    "Большая часть кода здесь будет легко понята тем, кто привык к Python.\n",
    "Мы находимся в процессе написания вступительной тетради для Джулии, которую мы надеемся скоро закончить, и с которой мы свяжемся здесь, когда закончим."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Модель\n",
    "\n",
    "Модель Бака-Снеппена[[3]](#bak-sneppen-paper) проста.\n",
    "Она дает нам некоторое базовое представление о процессе эволюции.\n",
    "У нас есть $N$ видов.\n",
    "Каждому виду приписывается коэффициент пригодности, случайное число от 0 до 1.\n",
    "Виды распределены по кольцу, т. е. по линии с периодическими граничными условиями.\n",
    "Идея модели состоит в том, чтобы применить к этой системе только очень простой набор правил и посмотреть, как она развивается.\n",
    "Моделирование состоит из двух этапов:\n",
    " 1. Найдите самый слабый вид, тот, у которого самый низкий коэффициент приспособленности.\n",
    " 2. Дайте этому виду и его двум соседям новые случайные факторы пригодности.\n",
    " \n",
    "На первый взгляд эти два правила могут показаться произвольными и немотивированными, но учтите следующее:\n",
    "Для подходящих видов изменение их фактора приспособленности (мутация) в среднем будет оказывать негативное влияние.\n",
    "Мутировавшее потомство подходящего вида, скорее всего, не принесет пользы этому виду и, таким образом, умрет.\n",
    "Однако менее приспособленные виды находятся под угрозой вымирания.\n",
    "Их мутировавшее потомство будет иметь больше шансов на выживание, чем их родители; мы позволяем первоначальному виду вымереть, а их мутировавшему потомству выжить.\n",
    "Мы назначаем новый (случайный) коэффициент пригодности мутировавшему потомству.\n",
    "Это мотивирует правило 1 (и первую часть правила 2).\n",
    "Виды в экосистеме не изолированы.\n",
    "Возмущения в какой-то части экосистемы могут повлиять на всю экосистему, даже на виды, которые вообще не взаимодействуют напрямую.\n",
    "Здесь мы ограничим взаимодействие ближайшими соседями - мы игнорируем косвенные эффекты.\n",
    "Представьте, что определенная порода кроликов в какой-то момент мутирует и становится меньше и быстрее.\n",
    "Для серой лисы и рыси, охотящихся на кролика, это угрожает их доступу к пище - кролик слишком быстр и проворен, чтобы его поймать.\n",
    "Однако маленькая серая лиса обнаруживает, что она достаточно мала, чтобы копаться в кроличьей норе, и поэтому в основном не подвержена изменениям.\n",
    "Рыси, однако, не так повезло.\n",
    "Она слишком велика, чтобы поместиться в кроличьей норе, и изо всех сил пытается найти достаточно пищи.\n",
    "Этот пример показывает, как мутация одного вида может по-разному повлиять на соседние виды.\n",
    "В нашей модели мы учитываем это, изменяя коэффициент пригодности двух ближайших соседей в экосистеме.\n",
    "Значение коэффициента пригодности двух ближайших соседей присваивается случайным образом, что означает, что эффект их мутировавшего соседа может быть как отрицательным, так и положительным.\n",
    "Это мотивирует правило 2."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Реализация\n",
    "\n",
    "Задача просто состоит в циклическом изменении вида.\n",
    "Этот тип проблем, итеративные шаги, где каждый шаг зависит от предыдущего, как правило, трудно реализовать с помощью NumPy, по крайней мере напрямую.\n",
    "Поскольку в конечном итоге мы напишем некоторую циклическую структуру, быстрый язык будет предпочтительнее Python'а - в нашем случае это Джулия.\n",
    "Сравнивая показанную здесь реализацию с аналогичной реализацией в Python, мы измерили, что реализация Julia примерно в два раза быстрее, чем в Python.\n",
    "\n",
    "\n",
    "Единственной программной проблемой, требующей решения, являются периодические граничные условия.\n",
    "Наш вид будет представлен в виде одномерного вектора с числами от 0 до 1, коэффициентами пригодности.\n",
    "Граничные условия играют роль только при нахождении соседей.\n",
    "Наивный, но функциональный подход заключается в том, чтобы для каждого шага проверять, находится ли он на границе, а затем выполнять специальный код для этих случаев.\n",
    "Однако мы используем общую технику, вдохновленную теорией графов.\n",
    "Каждый элемент вектора, узел $i$, имеет двух соседей, одного правого и одного левого, обычно узел $i+1$ и $i-1$.\n",
    "Мы создадим два вектора, один с правыми индексами и один с левыми индексами.\n",
    "Элемент в позиции $i$ в нашем правом индексном векторе будет иметь значение $i+1$, за исключением границы.\n",
    "Аналогично для указателя слева.\n",
    "Это позволяет писать очень чистый код. [<sup>1</sup>](#footnote-1)\n",
    "\n",
    "<center>\n",
    "<img src=\"images/index_fig.png\" alt=\"Index arrays\" /><br />\n",
    "</center>\n",
    "\n",
    "На рисунке показаны массивы `ir` и `il`, которые при индексе `i` дают индекс правого и левого элемента.\n",
    "\n",
    "По мере того как моделирование повторяется во времени, мы будем для каждой итерации сохранять минимальный коэффициент пригодности в этой итерации, а также возраст каждого вида. Мы можем интерпретировать возраст как годы, прошедшие с момента закрепления мутации.\n",
    "Возраст рассчитывается путем увеличения возраста на единицу для каждого раунда и установки его на ноль, когда вид \"мутирует\". "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "N = 1000  # Number of species.\n",
    "t_end = 100000  # End time of simulation.\n",
    "t_space = 1:t_end  # An iterable for the time steps. Note that Julia is 1-indexed.\n",
    "\n",
    "# Create the species' fitness factor.\n",
    "# rand(N) returns a vector of size N with random elements between 0.0 and 1.0.\n",
    "species = rand(N)\n",
    "\n",
    "# Fix boundary conditions\n",
    "# ir and il are right indices and left indices respectively\n",
    "# ir[i] gives the right index at position i, ususally i+1.\n",
    "#\n",
    "# `collect` converts a range, for example 2:N, into an array.\n",
    "# It serves the same purpose as doing `list(range(2, N))` in Python.\n",
    "ir = collect(2:N+1)\n",
    "il = collect(0:N-1)\n",
    "ir[N] = 1  # Right of rightmost is leftmost in a ring\n",
    "il[1] = N  # Left of leftmost is rightmost\n",
    "\n",
    "\"\"\"\n",
    "Simulate the species over the given time interval.\n",
    "Return the age of each species at each time step and \n",
    "the minimum fitness factor at each time step.\n",
    "\"\"\"\n",
    "function simulate!(species, t_space)\n",
    "    age = zeros(N, t_end)\n",
    "    mins = zeros(t_end)\n",
    "    \n",
    "    for t in t_space[2:end]\n",
    "        age[:, t] = age[:, t-1] .+ 1\n",
    "        # We want to find the weakest species.\n",
    "        # `findmin` returns the minimum value and the index of that value.\n",
    "        mins[t], index_min = findmin(species)\n",
    "        species[index_min] = rand()\n",
    "        species[ir[index_min]] = rand()  # Right\n",
    "        species[il[index_min]] = rand()  # Left\n",
    "        \n",
    "        age[index_min, t] = 0\n",
    "        age[ir[index_min], t] = 0  # Right\n",
    "        age[il[index_min], t] = 0  # Left\n",
    "    end\n",
    "    \n",
    "    return age, mins\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@time age, mins = simulate!(species, t_space);\n",
    "# We add a semicolon so that Jupter does not print the values of age and mins."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Джулия поддерживает несколько бэкендов для построения графиков.\n",
    "Здесь мы будем использовать PyPlot, который является просто интерфейсом к Matplotlib Python.\n",
    "Для любого, кто знаком с Matplotlib, следующий код должен быть легко понятен."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using PyPlot\n",
    "# There is a lot happening in this first line. Let's break it down.\n",
    "# The `[:, 1:100:end]` resembles Python's slicing syntax, but there are slight differences.\n",
    "# The first colon simply means every row, just like Python.\n",
    "# `1:100:end` follow the syntax `START:STEP:END`, so it means\n",
    "# 'from first column to last column, take every 100th column'.\n",
    "# `transpose` obviously transposes the matrix, this is done only\n",
    "# to have the the axis where we think they make the most sense,\n",
    "# ie. time on second axis.\n",
    "# `vmax` is the same as in matplotlib. It is included to\n",
    "# give better contrast in the figure, by setting all ages over\n",
    "# `vmax` to the same color, bringing out the details in\n",
    "# rapidly changing areas.\n",
    "plt.imshow(transpose(age[:, 1:100:end]), vmax=30000)\n",
    "plt.xlabel(\"Species\")\n",
    "plt.ylabel(\"Age [# iterations]\")\n",
    "plt.title(\"The age of species (x-axis) through the simulation.\")\n",
    "plt.show()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "# There is a lot happening in this first line. Let's break it down.\n",
    "# The `[:, 1:100:end]` resembles Python's slicing syntax, but there are slight differences.\n",
    "# The first colon simply means every row, just like Python.\n",
    "# `1:100:end` follow the syntax `START:STEP:END`, so it means\n",
    "# 'from first column to last column, take every 100th column'.\n",
    "# `transpose` obviously transposes the matrix, this is done only\n",
    "# to have the the axis where we think they make the most sense,\n",
    "# ie. time on second axis.\n",
    "# `vmax` is the same as in matplotlib. It is included to\n",
    "# give better contrast in the figure, by setting all ages over\n",
    "# `vmax` to the same color, bringing out the details in\n",
    "# rapidly changing areas.\n",
    "\n",
    "heatmap(transpose(age[:, 1:100:end]), vmax=30000)\n",
    "xaxis!(\"Species\")\n",
    "yaxis!(\"Age [# iterations]\")\n",
    "title!(\"The age of species (x-axis) through the simulation.\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "На рисунке выше показан возраст вида с течением времени.\n",
    "Обратите внимание на группировку видов, где пространственно близкие виды, как правило, выживают вместе в течение длительного времени.\n",
    "Можно рассматривать кластеры как устойчивые подэкосистемы. Эволюции, как правило, локализуются как во времени, так и в пространстве.\n",
    "Если важный вид в стабильной подэкосистеме внезапно мутирует, эффект этой единственной мутации распространяется по всей подэкосистеме, приводя к мутации у каждого вида, населяющего эту подэкосистему.\n",
    "Эта последовательность массовых эволюций называется лавинами, явление, которое много изучается в области статистической физики.  \n",
    "\n",
    "Теперь мы рассмотрим минимальный коэффициент пригодности в экосистеме как функцию времени.\n",
    "Мы видим, что он быстро увеличивается, а затем сужается до асимптотически стабильного значения, примерно равного 0.6.\n",
    "Это объясняет локализацию эволюций, которые мы видим на возрастном графике.\n",
    "Когда происходит мутация, вероятность того, что три новых сгенерированных значения будут ниже любого другого значения, довольно высока, по крайней мере, через некоторое время, когда минимальное значение будет высоким.\n",
    "\n",
    "Теперь, по общему признанию, следующий код довольно плотный.\n",
    "Можно было бы написать этот фрагмент таким образом, чтобы он был более понятен тем, кто не знаком с Джулией.\n",
    "Однако это решение короче, эффективнее, и в конечном счете мы надеемся, что оно может передать часть мощи Джулии.\n",
    "Итак, что происходит в этой единственной строке кода?\n",
    "Мы хотим посмотреть, как минимальные значения изменяются с течением времени.\n",
    "Наивный подход к простому построению значений `mins`, к сожалению, ничего не даст, так как слишком много статистического шума.\n",
    "Мы будем придерживаться более утонченного подхода.\n",
    "Разделим ось времени и возьмите максимальное значение `mins` в каждом разделе.\n",
    "Или, говоря по-другому, сгруппируем значения `mins` в \"корзины\" по N значений в каждой и возьмем максимум в каждой корзине.\n",
    "\n",
    "Затем построим эти значения.\n",
    "\"Утилиты итерации\" Джулии предлагают итератор `Iterators.partition`.\n",
    "Она делает именно то, что мы только что описали, при заданном некотором векторе, делит его на корзины, каждая из которых содержит некоторое заданное количество элементов.\n",
    "Таким образом, `Iterators.partition(mins, 500)` возвращает итератор.\n",
    "Каждый элемент в итераторе представляет собой вектор с 500 элементами, взятыми из `mins`.\n",
    "Затем мы просто берем максимальное значение этого вектора.\n",
    "Было сделано обоснованное предположение, чтобы прийти к цифре 500.\n",
    "Само по себе число не несет никакого физического значения;\n",
    "нам нужно какое-то значение, достаточно большое, чтобы отфильтровать шум, но достаточно малое, чтобы не удалять интересующие нас данные."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "basket_size = 500\n",
    "plot([maximum(a) for a in Iterators.partition(mins, basket_size)])\n",
    "xaxis!(\"Minimum fitness factor\")\n",
    "yaxis!(\"Time\")\n",
    "# Since we collected our results in baskets, the numbers on the time-axis are wrong.\n",
    "# We either have to rescale them or remove them.\n",
    "# Since we are mainly interested in the qualitative behavior, we remove the numbers."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Теперь мы признаем, что и модель, и анализ модели в этом блокноте намного проще, чем то, что мы обычно делаем в наших блокнотах.\n",
    "Однако цель этой записной книжки состоит в том, чтобы просто проиллюстрировать, что Джулия является жизнеспособным вариантом для написания современных эффективных вычислений.\n",
    "Мы надеемся, что вы нашли эту записную книжку несколько вдохновляющей и, надеюсь, она может послужить добрым знакомством с Джулией.\n",
    "\n",
    "## Сноски\n",
    "<span id=\"footnote-1\"><sup>1</span> Не так просто предсказать, приведет ли это к более быстрому времени выполнения или нет. В данном конкретном примере это на самом деле медленнее, чем использование условного подхода, т.е. выполнение специального кода внутри оператора `if`, если он находится на границе. Это было обнаружено простым внедрением обоих методов и измерением времени выполнения. В общем, предсказать, что быстрее, сложно, и если эффективность важна, рекомендуется экспериментировать с различными реализациями. Однако метод, проиллюстрированный здесь, предлагает гораздо более чистый код, что становится все более важным в более сложных задачах.</span>"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# References\n",
    " - <a name=\"f2py\">[1]</a> https://nbviewer.jupyter.org/urls/www.numfys.net/media/notebooks/fortran_to_python.ipynb\n",
    " - <a name=\"monte_carlo\">[2]</a> https://nbviewer.jupyter.org/urls/www.numfys.net/media/notebooks/Monte_Carlo_Ising.ipynb\n",
    " - <a name=\"bak-sneppen-paper\">[3]</a> Bak, Per & Sneppen, Kim. (1994). Bak, P. & Snepen, K. Punctuated equilibrium and criticality in a simple model of evolution. Physical review letters. 71. 4083-4086. 10.1103/PhysRevLett.71.4083. "
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "ipynb,md"
  },
  "kernelspec": {
   "display_name": "Julia 1.5.4",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
